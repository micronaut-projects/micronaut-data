buildscript {
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/micronaut/core-releases-local" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "io.micronaut.build:micronaut-gradle-plugins:2.0.11"
    }
}

subprojects { Project subproject ->
    group "io.micronaut.data"

    boolean isExampleOrBenchmark = subproject.parent.name == "docs-examples" || subproject.name.contains("example") || subproject.name.contains("benchmark")
    if (subproject.name != 'data-bom' || isExampleOrBenchmark) {
        apply plugin: "io.micronaut.build.common"

        test {
            environment("TESTCONTAINERS_RYUK_DISABLED", "true")
        }

        tasks.withType(JavaCompile) {
            options.encoding = "UTF-8"
            options.compilerArgs.addAll([
                    '-parameters',
                    // enables incremental compilation
                    '-Amicronaut.processing.incremental=true',
                    '-Amicronaut.processing.annotations=io.micronaut.data.*',
                    "-Amicronaut.processing.group=$project.group",
                    "-Amicronaut.processing.module=$project.name",
            ])
        }
    } else {
        apply plugin: "java-platform"
        version projectVersion
    }    


    if (isExampleOrBenchmark) {
        checkstyleMain.enabled = false
        tasks.withType(com.diffplug.gradle.spotless.SpotlessCheck) { it.enabled = false }
        return
    }

    apply plugin: "io.micronaut.build.dependency-updates"
    apply plugin: "io.micronaut.build.publishing"

    if (subproject.name != 'data-bom') {
        dependencies {
            testImplementation("org.spockframework:spock-core:${spockVersion}") {
                exclude module:'groovy-all'
            }
            testImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"        
        }
    }
    
}

apply plugin: "io.micronaut.build.docs"
apply plugin: "io.micronaut.build.dependency-updates"
