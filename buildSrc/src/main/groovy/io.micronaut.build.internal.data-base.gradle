import java.time.Duration

plugins {
    id "java-library"
}

repositories {
    mavenCentral()
}

group "io.micronaut.data"
version projectVersion

java {
    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8
}

configurations {
    all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.name == 'groovy-all') {
                throw new GradleException("Dependency 'groovy-all' not allowed!")
            }
            if (details.requested.group == 'io.micronaut') {
                details.useVersion "$micronautVersion"
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.compilerArgs.addAll([
            '-parameters',
            // enables incremental compilation
            '-Amicronaut.processing.incremental=true',
            '-Amicronaut.processing.annotations=io.micronaut.data.*,io.micronaut.core.annotation.*,javax.persistence.*,jakarta.persistence.*',
            "-Amicronaut.processing.group=$project.group",
            "-Amicronaut.processing.module=$project.name",
    ])
}

tasks.withType(Test).configureEach {
    jvmArgs '-Xmx2048m'
    systemProperty "micronaut.cloud.platform", "OTHER"
    if (JavaVersion.current() == JavaVersion.VERSION_15) {
        jvmArgs "--enable-preview"
    }
    timeout = Duration.ofHours(1)
}
