import org.gradle.api.services.BuildService
import org.gradle.api.services.BuildServiceParameters

import java.time.Duration

plugins {
    id "java-library"
    id "io.micronaut.build.internal.data-dependencies"
}

group "io.micronaut.data"
version projectVersion

tasks.withType(JavaCompile).configureEach {
    options.fork = true
    options.encoding = "UTF-8"
    options.compilerArgs.addAll([
            '-parameters',
            // enables incremental compilation
            '-Amicronaut.processing.incremental=true',
            '-Amicronaut.processing.annotations=io.micronaut.data.*,io.micronaut.core.annotation.*,javax.persistence.*,jakarta.persistence.*',
            "-Amicronaut.processing.group=$project.group",
            "-Amicronaut.processing.module=$project.name",
    ])
}

tasks.withType(Test).configureEach {
    systemProperty "micronaut.cloud.platform", "OTHER"
    timeout = Duration.ofHours(1)
    if (micronautBuild.environment.githubAction.get()) {
        plugins.withId("io.micronaut.test-resources") {
            finalizedBy("stopTestResourcesService")
        }
    }
}

configurations.configureEach {
    resolutionStrategy.preferProjectModules()
}
