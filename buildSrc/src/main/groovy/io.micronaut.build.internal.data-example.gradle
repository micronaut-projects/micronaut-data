plugins {
    id "io.micronaut.build.internal.data-dependencies"
    id "io.micronaut.application"
}

configurations.all {
    resolutionStrategy.dependencySubstitution { DependencySubstitutions subs ->
        project.rootProject.subprojects {
            if (!it.name.startsWith("data") || it.name.startsWith("data-bom")) {
                return
            }
            substitute(module("io.micronaut.data:micronaut-${it.name}")).using(subs.project(it.path))
        }
    }
}

tasks.named("test") {
    onlyIf {
        def disabled = project.path.contains("groovy") && JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_16)
        !disabled
    }
}

tasks.register("testExamples") {
    dependsOn "test"
}

tasks.withType(JavaCompile).configureEach {
    options.fork = true
}

project.afterEvaluate {
    nativeCompile.enabled = false
    testNativeImage.enabled = false
}

dependencies {
    runtimeOnly mn.snakeyaml
}
