plugins {
    id "io.micronaut.build.internal.data-dependencies"
    id "io.micronaut.application"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations.all {
    resolutionStrategy.dependencySubstitution { DependencySubstitutions subs ->
        project.rootProject.subprojects {
            if (!it.name.startsWith("data") || it.name.startsWith("data-bom")) {
                return
            }
            substitute(module("io.micronaut.data:micronaut-${it.name}")).using(subs.project(it.path))
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.fork = true
}

if (micronautBuild.environment.githubAction.get()) {
    def waitForTasks = [
            ":data-hibernate-jpa:test",
            ":data-jdbc:test",
            ":data-r2dbc:test",
            ":data-mongodb:test"
    ] as Object[]
    // Let core modules tests pass first so the test containers don't eat all the memory
    tasks.named("compileJava") {
        mustRunAfter waitForTasks
    }
    tasks.named("processResources") {
        mustRunAfter waitForTasks
    }
    def compileKotlinTask = tasks.findByName("compileKotlin")
    if (compileKotlinTask) {
        compileKotlinTask.mustRunAfter waitForTasks
    }
    def internalStartTestResourcesServiceTask = tasks.findByName("internalStartTestResourcesService")
    if (internalStartTestResourcesServiceTask) {
        internalStartTestResourcesServiceTask.mustRunAfter waitForTasks
    }
    def kaptGenerateStubsKotlinTask = tasks.findByName("kaptGenerateStubsKotlin")
    if (kaptGenerateStubsKotlinTask) {
        kaptGenerateStubsKotlinTask.mustRunAfter waitForTasks
    }
    tasks.named("test") {
        mustRunAfter waitForTasks
    }
}

tasks.register("testExamples") {
    dependsOn "test"
}

project.afterEvaluate {
    nativeCompile.enabled = false
    testNativeImage.enabled = false
}

dependencies {
    testRuntimeOnly mnSerde.micronaut.serde.jackson
    runtimeOnly mn.snakeyaml
}
