plugins {
    id "java"
    id "io.micronaut.application"
    id "io.micronaut.test-resources"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.register("testExamples") {
    dependsOn "test"
}

dependencies {
    micronautBoms(
            platform("io.micronaut:micronaut-core-bom:$micronautVersion"),
            platform(mnTest.micronaut.test.bom)
    )
    testRuntimeOnly mnSerde.micronaut.serde.jackson
    runtimeOnly mn.snakeyaml
}

configurations.configureEach {
    resolutionStrategy.preferProjectModules()
}

graalvmNative {
    toolchainDetection = false
    metadataRepository {
        enabled = true
    }
    binaries {
        all {
            logger.quiet("GRAALVM_HOME:  {}", (String) System.getenv("GRAALVM_HOME"))
            if (System.getenv("GRAALVM_HOME")?.contains("graal") && System.getenv("GRAALVM_HOME")?.contains("21")) {
                logger.quiet("Enabling strict image heap!")
                buildArgs.add("--strict-image-heap")
            }
        }
    }
}

