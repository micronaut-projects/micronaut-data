plugins {
    id "io.micronaut.build.internal.data-module"
}

dependencies {
    annotationProcessor mn.micronaut.inject.java
    annotationProcessor mn.micronaut.graal
    annotationProcessor libs.micronaut.docs

    implementation platform(libs.r2dbc.bom)
    testImplementation platform(libs.r2dbc.bom)

    api mn.micronaut.reactor
    api projects.dataModel
    api projects.dataRuntime
    api libs.micronaut.r2dbc
    api libs.r2dbc.spi

    implementation mn.micronaut.http
    implementation mn.micronaut.reactor
//    implementation "io.projectreactor.netty:reactor-netty"

    testImplementation projects.dataTck
    testImplementation mn.micronaut.test.spock
    testImplementation libs.r2dbc.pool
    testImplementation mn.micronaut.rxjava2

    testAnnotationProcessor mn.micronaut.inject.java
    testAnnotationProcessor projects.dataProcessor

    testImplementation mn.jackson.databind
    testImplementation libs.javax.persistence.api
    testImplementation libs.jakarta.persistence.api
    testImplementation libs.groovy.sql
    testImplementation mn.micronaut.validation
    testImplementation libs.testcontainers.r2dbc

    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_11)) {
        testImplementation libs.drivers.r2dbc.oracle
    }
    testImplementation libs.testcontainers.oracle

    // mariadb
    testImplementation libs.testcontainers.mariadb
    testImplementation libs.drivers.r2dbc.mariadb
    testImplementation libs.drivers.jdbc.mariadb

    // mysql
    testImplementation libs.testcontainers.mysql
    testImplementation libs.drivers.r2dbc.mysql
    testImplementation libs.drivers.jdbc.mysql

    // h2
    testImplementation libs.r2dbc.h2
    testRuntimeOnly libs.h2

    // sqlserver
    testImplementation libs.testcontainers.mssqlserver
    testImplementation libs.drivers.r2dbc.mssql
    testImplementation libs.drivers.jdbc.mssql

    // postgres
    testImplementation libs.testcontainers.postgresql
    testImplementation libs.drivers.r2dbc.postgresql
    testImplementation libs.drivers.jdbc.postgresql

    testCompileOnly mn.micronaut.inject.groovy
    testCompileOnly projects.dataProcessor
}

test {
    systemProperty "oracle.jdbc.timezoneAsRegion", "false"
    systemProperty "java.net.preferIPv4Stack", "true"
    systemProperty "java.util.concurrent.ForkJoinPool.common.parallelism", "2"
}

micronautBuild {
    resolutionStrategy {
        componentSelection {
            all { selection ->
                if (selection.candidate.module == 'mssql-jdbc' && !selection.candidate.version.contains("jre8")) {
                    selection.reject("Not a JDK8 compatible MS SQL driver")
                }
            }
        }
    }
}
