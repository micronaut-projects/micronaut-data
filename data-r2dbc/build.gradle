plugins {
    id "io.micronaut.build.internal.data-module"
}

dependencies {
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-graal"
    annotationProcessor "io.micronaut.docs:micronaut-docs-asciidoc-config-props:$micronautDocsVersion"
    implementation "io.micronaut.reactor:micronaut-reactor"
    implementation "io.projectreactor.netty:reactor-netty"

    implementation platform("io.r2dbc:r2dbc-bom:$r2dbcVersion")
    testImplementation platform("io.r2dbc:r2dbc-bom:$r2dbcVersion")

    api "io.projectreactor:reactor-core"
    api project(":data-model")
    api project(":data-runtime")
    implementation "io.micronaut:micronaut-http"
    api "io.micronaut.r2dbc:micronaut-r2dbc-core:$micronautR2dbcVersion"
    api "io.r2dbc:r2dbc-spi"

    testImplementation project(":data-tck")
    testImplementation "io.micronaut.test:micronaut-test-spock"
    testImplementation "io.r2dbc:r2dbc-pool"
    testImplementation 'io.micronaut.rxjava2:micronaut-rxjava2'

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testAnnotationProcessor project(":data-processor")
    testCompileOnly "io.micronaut:micronaut-inject-groovy"
    testCompileOnly project(":data-processor")
    testImplementation "com.fasterxml.jackson.core:jackson-databind"
    testImplementation "javax.persistence:javax.persistence-api:$javaxPersistenceVersion"
    testImplementation "jakarta.persistence:jakarta.persistence-api:$jakartaPersistenceVersion"
    testImplementation "org.codehaus.groovy:groovy-sql:$groovyVersion"
    testImplementation "io.micronaut:micronaut-validation"
    testImplementation "org.testcontainers:r2dbc"

    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_11)) {
        // oracle
        def oracleVersion = project.ext.r2dbcOracleVersion
        if (oracleVersion) {
            testImplementation "com.oracle.database.r2dbc:oracle-r2dbc:$oracleVersion"
        } else {
            testImplementation "com.oracle.database.r2dbc:oracle-r2dbc"
        }
    }
    testImplementation "org.testcontainers:oracle-xe"

    // mariadb
    testImplementation 'org.mariadb:r2dbc-mariadb'
    testImplementation "org.testcontainers:mariadb"
    testImplementation 'org.mariadb.jdbc:mariadb-java-client'

    // mysql
    testImplementation "org.testcontainers:mysql"
    testImplementation 'mysql:mysql-connector-java'
    testImplementation 'dev.miku:r2dbc-mysql:0.8.2.RELEASE'

    // h2
    testImplementation 'io.r2dbc:r2dbc-h2'
    testRuntimeOnly "com.h2database:h2"

    // sqlserver
    testImplementation "org.testcontainers:mssqlserver"
    testImplementation 'com.microsoft.sqlserver:mssql-jdbc'
    testImplementation 'io.r2dbc:r2dbc-mssql'

    // postgres
//    testImplementation 'org.postgresql:r2dbc-postgresql'
    testImplementation 'io.r2dbc:r2dbc-postgresql'
    testImplementation "org.testcontainers:postgresql"
    testImplementation 'org.postgresql:postgresql'
}

test {
    systemProperty "oracle.jdbc.timezoneAsRegion", "false"
    systemProperty "java.net.preferIPv4Stack", "true"
    systemProperty "java.util.concurrent.ForkJoinPool.common.parallelism", "2"
}

micronautBuild {
    resolutionStrategy {
        componentSelection {
            all { selection ->
                if (selection.candidate.module == 'mssql-jdbc' && !selection.candidate.version.contains("jre8")) {
                    selection.reject("Not a JDK8 compatible MS SQL driver")
                }
            }
        }
    }
}
