import io.micronaut.testresources.buildtools.KnownModules

plugins {
    id "groovy"
    id "io.micronaut.build.internal.data-example"
    id "io.micronaut.test-resources"
}

application {
    mainClass = "example.Application"
}

micronaut {
    version libs.versions.micronaut.platform.get()
    runtime "netty"
    testRuntime "spock"
    testResources {
        enabled = true
        inferClasspath = false
        additionalModules.add(KnownModules.HIBERNATE_REACTIVE_POSTGRESQL)
        clientTimeout = 300
        version = libs.versions.micronaut.testresources.get()
    }
}

dependencies {
    compileOnly projects.micronautDataProcessor // because it's Groovy
    implementation mn.micronaut.http.validation
    implementation projects.micronautDataHibernateReactive
    implementation mnReactor.micronaut.reactor
    runtimeOnly mnLogging.logback.classic
    runtimeOnly mnSql.vertx.pg.client

    testResourcesService mnSql.postgresql
    // See https://github.com/eclipse-vertx/vertx-sql-client/issues/1059
    testRuntimeOnly "com.ongres.scram:client:2.1"
}
